/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    Config file for defining DSL2 per module options and publishing paths
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    Available keys to override module options:
        ext.args   = Additional arguments appended to command in module.
        ext.args2  = Second set of arguments appended to command in module (multi-tool modules).
        ext.args3  = Third set of arguments appended to command in module (multi-tool modules).
        ext.prefix = File name prefix for output files.
----------------------------------------------------------------------------------------
*/

process {

    // Default values
    publishDir = [
        path: { "${params.outdir}" },
        mode: params.publish_dir_mode,
        saveAs: { filename -> filename.equals("versions.yml") ? null : filename }
    ]

    withName: SAMPLESHEET_CHECK {
        publishDir = [
            path: { "${params.outdir}/pipeline_info" },
            mode: params.publish_dir_mode,
            saveAs: { filename -> filename.equals("versions.yml") ? null : filename }
        ]
    }

    //*
    // DEMULTIPLEX AND TRIMMING
    //*

    withName: BCLCONVERT {
        ext.args = {[
            meta.lane ? "--bcl-only-lane ${meta.lane}" : "",
            "--force",
            "--strict-mode true"
        ].join(" ").trim()}
        publishDir = [
            [
                // Gather and write InterOp files
                path: { "${params.outdir}/InterOp" },
                mode: params.publish_dir_mode,
                pattern: "**.bin",
                saveAs: {filename -> filename.split("/")[-1] }
            ],
            [
                // Fetch RunInfo.xml
                path: { "${params.outdir}/Reports/L00${meta.lane}" },
                mode: params.publish_dir_mode,
                pattern: "RunInfo.xml",
                overwrite: true,
                saveAs: {filename -> filename.split("/")[-1] }
            ],
            [
                // Gather and write Reports
                path: { "${params.outdir}/Reports/L00${meta.lane}" },
                mode: params.publish_dir_mode,
                pattern: "Reports/*",
                saveAs: {filename -> filename.split("/")[-1] }
            ],
            [
                // don't write the fastq's
                pattern: "**.fastq.gz",
                enabled: false
            ]
        ]
    }

    withName: UNTAR {
        publishDir = [ enabled: false ]
    }

    withName: FASTP {
        ext.args = [
            "--detect_adapter_for_pe",
            params.skip_trimming    ? "-A" : "",
            params.trim_front   > 0 ? "--trim_front1 ${params.trim_front}" : "",
            params.trim_tail    > 0 ? "--trim_tail1  ${params.trim_tail}" : "",
            params.adapter_R1       ? "--adapter_sequence ${params.adapter_R1}" : "",
            params.adapter_R2       ? "--adapter_sequence ${params.adapter_R2}" : "",
        ].join(" ").trim()
        publishDir = [
            [
                path: { "${params.outdir}/${meta.samplename}" },
                mode: params.publish_dir_mode,
                pattern: "*.{html,json}"
            ],
        ]
    }

    //*
    // ALIGNMENT
    //*
    withName: BOWTIE2_ALIGN {
        ext.args = "--local --fast-local"
        publishDir = [ enabled: false ]
    }

    //*
    // BAMPROCESSING
    //*
    withName: BAMTOOLS_SPLIT {
        ext.args = {[
            "-reference",
        ].join(" ").trim()}
        publishDir = [ enabled: false ]
    }

    withName: BIOBAMBAM_BAMMARKDUPLICATES2 {
        ext.prefix = {"${meta.id}.markdup"}
        ext.args = {[
            "index=1",
            "indexfilename=${meta.id}.markdup.bam.bai",
            "optminpixeldif=2500"
        ].join(" ").trim()}
        publishDir = [
            [
                path: { "${params.outdir}/${meta.samplename}" },
                mode: params.publish_dir_mode,
                pattern: "*metrics*",
                saveAs: {filename -> filename.replace("metrics", "duplicate_metrics")}
            ],
        ]
    }

    withName: BIOBAMBAM_BAMMERGE {
        ext.prefix = {"${meta.id}.merged"}
        ext.args = {[
            "index=1",
            "indexfilename=${meta.id}.merged.bam.bai",
        ].join(" ").trim()}
        publishDir = [ enabled: false ]
    }

    withName: BIOBAMBAM_BAMSORMADUP {
        ext.prefix = {"${meta.id}.merged"}
        ext.args = {[
            "indexfilename=${meta.id}.merged.bam.bai",
        ].join(" ").trim()}
        publishDir = [
            [
                path: { "${params.outdir}/${meta.samplename}" },
                mode: params.publish_dir_mode,
                pattern: "*metrics*",
                saveAs: {filename -> filename.replace("metrics", "duplicate_metrics")}
            ],
        ]
    }

    withName: SAMTOOLS_CONVERT {
        // activate cram compression
        ext.args = "-C --output-fmt-option archive"
        publishDir = [
            [
                path: { "${params.outdir}/${meta.samplename}" },
                mode: params.publish_dir_mode,
                saveAs: { filename -> filename.equals("versions.yml") ? null : filename }
            ],
        ]
    }

    //*
    // COVERAGE
    //*

    withName: ".*:COVERAGE:.*" {
        ext.when = {params.run_coverage}
    }

    withName: MOSDEPTH {
        ext.args = [
            "--no-per-base",
            "--fast-mode" ,
            "--quantize 0:1:4:100:200:"
        ].join(" ").trim()
    }

    //*
    // QC
    //*

    withName: ".*:PICARD.*" {
        publishDir = [
            [
                path: { "${params.outdir}/${meta.samplename}" },
                mode: params.publish_dir_mode,
                saveAs: { filename -> filename.equals("versions.yml") ? null : filename }
            ],
        ]
    }

    withName: CUSTOM_DUMPSOFTWAREVERSIONS {
        publishDir = [
            path: { "${params.outdir}/pipeline_info" },
            mode: params.publish_dir_mode,
            pattern: "*_versions.yml"
        ]
    }

}
